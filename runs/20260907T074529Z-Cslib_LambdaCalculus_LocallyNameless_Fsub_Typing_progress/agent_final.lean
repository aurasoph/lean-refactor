/-
Copyright (c) 2025 Chris Henson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Henson
-/

module

public import Cslib.Languages.LambdaCalculus.LocallyNameless.Fsub.Typing

/-! # λ-calculus

The λ-calculus with polymorphism and subtyping, with a locally nameless representation of syntax.
This file proves type safety.

## References

* [A. Chargueraud, *The Locally Nameless Representation*][Chargueraud2012]
* See also <https://www.cis.upenn.edu/~plclub/popl08-tutorial/code/>, from which
  this is adapted

-/

public section

namespace Cslib

variable {Var : Type*} [HasFresh Var] [DecidableEq Var]

namespace LambdaCalculus.LocallyNameless.Fsub

open Context List Env.Wf Term Ty

variable {t : Term Var}

/-- Any reduction step preserves typing. -/
lemma Typing.preservation (der : Typing Γ t τ) (step : t ⭢βᵛ t') : Typing Γ t' τ := by
  induction der generalizing t'
  case app Γ _ σ τ _ _ _ _ _ =>
    cases step
    case appₗ | appᵣ => grind
    case abs der _ _ =>
      have sub : Sub Γ (σ.arrow τ) (σ.arrow τ) := by grind [Sub.refl]
      have ⟨_, _, ⟨_, _⟩⟩ := der.abs_inv sub
      grind [fresh_exists <| free_union [fvTm] Var, openTm_substTm_intro, subst_tm, Sub.weaken]
  case tapp Γ _ σ τ σ' _ _ _ =>
    cases step
    case tabs der _ _ =>
      have sub : Sub Γ (σ.all τ) (σ.all τ) := by grind [Sub.refl]
      have ⟨_, _, ⟨_, _⟩⟩ := der.tabs_inv sub
      have ⟨X, mem⟩ := fresh_exists <| free_union [Ty.fv, fvTy] Var
      simp at mem
      have : Γ = (Context.mapVal (·[X := σ']) []) ++ Γ := by grind
      rw [openTy_substTy_intro (X := X), open_subst_intro (X := X)] <;> grind [subst_ty]
    case tapp => grind
  case let' Γ _ _ _ _ L der _ ih₁ _ =>
    cases step
    case let_bind red₁ _ => apply Typing.let' L (ih₁ red₁); grind
    case let_body =>
      grind [fresh_exists <| free_union [fvTm] Var, openTm_substTm_intro, subst_tm]
  case case Γ _ σ τ _ _ _ L _ _ _ ih₁ _ _ =>
    have sub : Sub Γ (σ.sum τ) (σ.sum τ) := by grind [Sub.refl]
    have : Γ = [] ++ Γ := by rfl
    cases step
    case «case» red₁ _ _ => apply Typing.case L (ih₁ red₁) <;> grind
    case case_inl der _ _ =>
      have ⟨_, ⟨_, _⟩⟩ := der.inl_inv sub
      grind [fresh_exists <| free_union [fvTm] Var, openTm_substTm_intro, subst_tm]
    case case_inr der _ _ =>
      have ⟨_, ⟨_, _⟩⟩ := der.inr_inv sub
      grind [fresh_exists <| free_union [fvTm] Var, openTm_substTm_intro, subst_tm]
  all_goals grind [cases Red]


lemma Typing.progress (der : Typing [] t τ) : t.Value ∨ ∃ t', t ⭢βᵛ t' := by
  generalize eq : [] = Γ at der
  have lc := der.wf.2.1
  induction der <;> subst eq
  case var => grind
  case abs => exact .inl (.abs lc)
  case tabs => exact .inl (.tabs lc)
  case sub der _ ih => exact ih rfl lc
  case app t₁ _ _ t₂ l r ih_l ih_r =>
    right
    rcases ih_l rfl l.wf.2.1 with vl | ⟨t', h⟩
    · rcases ih_r rfl r.wf.2.1 with vr | ⟨t', h⟩
      · obtain ⟨_, _, rfl⟩ := l.canonical_form_abs vl
        exact ⟨_, .abs vl.lc vr⟩
      · exact ⟨_, .appᵣ vl h⟩
    · exact ⟨_, .appₗ r.wf.2.1 h⟩
  case tapp σ' der sub ih =>
    have hσ := sub.wf.2.1.lc
    rcases ih rfl der.wf.2.1 with v | ⟨t', h⟩
    · obtain ⟨_, _, rfl⟩ := der.canonical_form_tabs v
      exact .inr ⟨_, .tabs v.lc hσ⟩
    · exact .inr ⟨_, .tapp hσ h⟩
  case case t₁ _ _ t₂ _ t₃ _ der _ _ ih _ _ =>
    obtain ⟨_, h₂, h₃⟩ := body_case.mp lc
    rcases ih rfl der.wf.2.1 with v | ⟨t', h⟩
    · obtain ⟨_, h | h⟩ := der.canonical_form_sum v <;> subst h
      all_goals grind only [cases Value, Red.case_inl, Red.case_inr]
    · exact .inr ⟨_, .case h h₂ h₃⟩
  case let' t₁ σ t₂ τ L der _ ih _ =>
    have hb := (body_let.mp lc).2
    rcases ih rfl der.wf.2.1 with v | ⟨t', h⟩
    all_goals solve_by_elim [Or.inr, Exists.intro, Red.let_body, Red.let_bind]
  case inl der _ ih | inr der _ ih =>
    rcases ih rfl der.wf.2.1 with v | ⟨t', h⟩
    all_goals solve_by_elim [Or.inl, Or.inr, Exists.intro, Value.inl, Value.inr, Red.inl, Red.inr]
