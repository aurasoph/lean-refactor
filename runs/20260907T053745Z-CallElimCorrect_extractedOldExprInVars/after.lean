/-
  Copyright Strata Contributors

  SPDX-License-Identifier: Apache-2.0 OR MIT
-/

import Init.Data.List.Basic
import Init.Data.List.Lemmas
import Strata.Languages.Core.Env
import Strata.Languages.Core.Identifiers
import Strata.Languages.Core.Program
import Strata.Languages.Core.ProgramType
import Strata.Languages.Core.WF
import Strata.DL.Lambda.Lambda
import Strata.Transform.CoreTransform
import Strata.Transform.CallElim
import Strata.DL.Imperative.CmdSemantics
import Strata.Languages.Core.StatementSemantics
import Strata.Languages.Core.StatementSemanticsProps
import Strata.DL.Util.ListUtils

/-! # Call Elimination Correctness Proof (DEPRECATED)

  We are deprecating this proof because it relies on the old big-step semantics for
  `Stmt`. This proof will be re-done with a new small-step semantics in the near
  future.

  This file contains the main proof that the call elimination transformation is
  semantics preserving (see `callElimStatementCorrect`).
  Additionally, `callElimBlockNoExcept` shows that the call elimination
  transformation always succeeds on well-formed statements.
-/

namespace CallElimCorrect
open Core Core.Transform CallElim

theorem CoreIdent.isGlob_isGlobOrLocl :
  PredImplies (CoreIdent.isGlob ·) (CoreIdent.isGlobOrLocl ·) := by
  intros x H
  simp [CoreIdent.isGlobOrLocl]
  exact Or.symm (Or.inr H)

theorem CoreIdent.isLocl_isGlobOrLocl :
  PredImplies (CoreIdent.isLocl ·) (CoreIdent.isGlobOrLocl ·) := by
  intros x H
  simp [CoreIdent.isGlobOrLocl]
  exact Or.symm (Or.inl H)

theorem CoreIdent.Disjoint_isTemp_isGlobOrLocl :
  PredDisjoint (CoreIdent.isTemp ·) (CoreIdent.isGlobOrLocl ·) := by
  intros x H1 H2
  simp [CoreIdent.isTemp] at H1
  simp [CoreIdent.isGlobOrLocl] at H2
  split at H1 <;> simp_all
  cases H2 <;> simp [CoreIdent.isGlob, CoreIdent.isLocl] at *

theorem CoreIdent.Disjoint_isLocl_isGlob :
  PredDisjoint (CoreIdent.isLocl ·) (CoreIdent.isGlob ·) := by
  intros x H1 H2
  simp [CoreIdent.isLocl] at H1
  simp [CoreIdent.isGlob] at H2
  split at H1 <;> simp_all

-- inidividual lemmas

theorem createHavocsApp :
createHavocs (a ++ b) = createHavocs a ++ createHavocs b := by
simp [createHavocs]

theorem createFvarsApp :
createFvars (a ++ b) = createFvars a ++ createFvars b := by
simp [createFvars]

theorem createFvarsLength :
(createFvars ls).length = ls.length := by
induction ls <;> simp [createFvars]

theorem filterIsGlobal_isSome :
  ident ∈ List.filter (isGlobalVar p) l →
  (p.find? DeclKind.var ident).isSome = true := by
  intros Hin
  simp [isGlobalVar] at Hin
  exact Hin.2

theorem getOldExprIdentTy_some : ∀ {p : Program} {id : Expression.Ident},
  (p.find? .var id).isSome = (getIdentTy? p id).isSome := by
  intros p id
  simp [Program.find?, getIdentTy?, Program.getVarTy?]
  split <;> simp_all

theorem getIdentTy!_store_same :
  getIdentTy! p x s = (Except.ok a', s') →
  s = s' := by
  intros H
  simp [getIdentTy!] at H
  generalize Hgt: (getIdentTy? p x) = gt at H
  split at H
  . cases H
  . simp [pure] at H
    cases H
    rfl

theorem getIdentTys!_store_same :
  getIdentTys! p xs s = (Except.ok a', s') →
  s = s' := by
  intros H
  induction xs generalizing s a' s' <;> simp [getIdentTys!, pure, ExceptT.pure, ExceptT.mk, StateT.pure] at H
  case nil =>
    cases H
    rfl
  case cons h t ih =>
    simp [bind, ExceptT.bind, ExceptT.mk, StateT.bind, ExceptT.bindCont] at H
    split at H
    next heq =>
    split at H
    . simp [StateT.bind, bind] at H
      split at H
      next heq' =>
      simp [ExceptT.bindCont] at H
      split at H
      . have H1 := getIdentTy!_store_same heq
        have H2 := ih heq'
        simp [StateT.pure, pure] at H
        cases H
        simp_all
      . cases H
    . cases H

theorem getIdentTy!_no_throw :
  (p.find? .var ident).isSome = true →
  ∃ r, (runWith ident (getIdentTy! p) cs).fst = (Except.ok r) := by
  intros H
  simp [runWith, StateT.run, getIdentTy!]
  have Hsome := @getOldExprIdentTy_some p ident
  simp [H] at Hsome
  simp [Option.isSome] at Hsome
  split at Hsome <;> simp_all
  next x val heq =>
  simp [pure, ExceptT.pure, ExceptT.mk, StateT.pure]

theorem getIdentTys!_no_throw :
  ∀ {p : Program}
    {idents : List Expression.Ident}
    {cs : CoreGenState},
  (∀ ident ∈ idents, (p.find? .var ident).isSome = true) →
  ∃ r, (runWith idents (getIdentTys! p) cs).fst = (Except.ok r) := by
  intros p idents cs Hglob
  induction idents generalizing cs
  case nil =>
    simp [getIdentTys!,
          pure, ExceptT.pure, ExceptT.mk,
          StateT.run, StateT.pure]
  case cons h t ih =>
    simp [getIdentTys!, bind, ExceptT.bind, ExceptT.mk, ExceptT.bindCont, StateT.run,
      StateT.bind] at *
    have Hsome : (p.find? DeclKind.var h).isSome = true := by simp_all
    have Hhead := @getIdentTy!_no_throw _ _ cs Hsome
    cases Hhead with
    | intro T' Hok' =>
    simp [runWith, StateT.run] at Hok'
    split <;> simp_all
    next err cs' Hres =>
    specialize @ih cs'
    cases ih with
    | intro T Hok =>
    simp [bind, StateT.bind, pure, ExceptT.pure, ExceptT.mk, ExceptT.bindCont]
    split <;> simp_all
    simp [pure, StateT.pure]

-- Step 1. A theorem stating that given a well-formed program, call-elim will return no exception
theorem callElimBlockNoExcept :
  ∀ (st : Core.Statement)
    (p : Core.Program),
    WF.WFStatementsProp p [st] →
  ∃ sts, Except.ok sts = ((run [st] (CallElim.callElimStmts · p)))
  -- NOTE: the generated variables will not be local, but temp. So it will not be well-formed
  -- ∧ WF.WFStatementsProp p sts
  := by
  intros st p wf
  simp [Transform.run, runStmts, CallElim.callElimStmts, CallElim.callElimCmd]
  cases st with
  | block l b md => exists [.block l b md]
  | ite cd tb eb md => exists [.ite cd tb eb md]
  | goto l b => exists [.goto l b]
  | loop g m i b md => exists [.loop g m i b md]
  | cmd c =>
    cases c with
    | cmd c' => exists [Imperative.Stmt.cmd (CmdExt.cmd c')]
    | call lhs procName args md =>
    split
    . -- call case
      next heq =>
      cases heq
      next st =>
      simp only [] -- reduce match
      split <;>
        simp only [StateT.run, bind, ExceptT.bind, ExceptT.mk, StateT.bind, genArgExprIdentsTrip, ne_eq, liftM,
              monadLift, MonadLift.monadLift, ExceptT.lift, Functor.map, List.unzip_snd, ite_not, ExceptT.bindCont, ExceptT.map,
              genOldExprIdentsTrip]
      . split
        next res a s heq1 =>
        split
        . -- succeeded, prove it is well-formed
          simp [StateT.bind, pure, Functor.map, ExceptT.mk, genOutExprIdentsTrip, liftM, monadLift,
            MonadLift.monadLift, ExceptT.lift, bind, ExceptT.bind, ExceptT.bindCont, StateT.bind]
          split at heq1 <;> try cases heq1
          . next res' a' s' heq2 =>
            split
            split <;> simp only [bind, StateT.bind, StateT.pure]
            . split
              sorry
              /-
              split <;> simp [pure, StateT.pure, Except.ok.injEq]
              -- old expression returns error, contradiction by well-formedness
              next ss _ _ _ _ s x e heq1 =>
              split at heq1 <;> simp_all
              next a' s' hif heq' =>
              cases heq' <;> simp_all
              simp [bind, ExceptT.bindCont, StateT.bind] at heq1
              split at heq1 <;> simp_all
              split at heq1
              . simp [ExceptT.pure, pure, ExceptT.mk, StateT.pure] at heq1
                cases heq1
              . next s x e heq =>
                generalize Heq : (List.filter (isGlobalVar p)
                  (List.flatMap OldExpressions.extractOldExprVars
                    (OldExpressions.normalizeOldExprs
                      (List.map Procedure.Check.expr res'.spec.postconditions.values))).eraseDups)
                        = eq at *
                have Hgen := @getIdentTys!_no_throw p eq (List.mapM.loop genOldExprIdent eq [] ss).snd ?_
                simp [runWith, StateT.run] at Hgen
                . cases Hgen with
                  | intro tys Hgen =>
                  simp_all
                . simp [← Heq, isGlobalVar] at *
              -/
            . -- output length not equal, contradiction
              next x e heq1 =>
              split at heq1
              . next x e heq1' =>
                simp [StateT.bind, StateT.map, pure, ExceptT.pure, ExceptT.bindCont, ExceptT.mk] at heq1
                cases heq1
              . -- lhs length not equal to outputs length
                next Hne =>
                cases wf with
                | mk df al ol =>
                exfalso
                apply Hne
                simp [Option.isSome] at df
                unfold CoreIdent.unres at *
                split at df <;> simp_all
                apply Hne
                simp [← ol, Lambda.LMonoTySignature.toTrivialLTy]
        . -- failed to get type for arguments, contradiction
          split at heq1
          . next x e heq1' =>
            simp [StateT.bind, StateT.map, pure, ExceptT.pure, ExceptT.bindCont, ExceptT.mk] at heq1
            cases heq1
          . -- arg length not equal to inputs length
            next Hne =>
            cases wf with
            | mk df al ol =>
            exfalso
            apply Hne
            simp [Option.isSome] at df
            unfold CoreIdent.unres at *
            split at df <;> simp_all
            apply Hne
            simp [← al, Lambda.LMonoTySignature.toTrivialLTy]
      . exfalso
        next proc Hfalse =>
        simp [Program.Procedure.find?] at Hfalse
        split at Hfalse <;> simp_all
        next heq' =>
        cases wf with
        | intro wf =>
        cases wf with
        | mk wf =>
        simp [Program.Procedure.find?] at wf
        unfold CoreIdent.unres at *
        split at wf <;> simp_all
    . -- other case
      grind


theorem postconditions_subst_unwrap :
  substPost ∈
  OldExpressions.substsOldExprs (createOldVarsSubst oldTrips)
    (OldExpressions.normalizeOldExprs ps) →
  ∃ post, post ∈ ps ∧ substPost = (OldExpressions.substsOldExpr (createOldVarsSubst oldTrips)
    (OldExpressions.normalizeOldExpr post)) := by
  intros H
  induction ps
  case nil =>
    simp [OldExpressions.normalizeOldExprs, OldExpressions.substsOldExprs] at H
  case cons h t ih =>
    simp [OldExpressions.normalizeOldExprs, OldExpressions.substsOldExprs] at H
    cases H with
    | inl Hin =>
      simp_all
    | inr Hin =>
      simp
      cases Hin with
      | intro x Hin =>
      right
      refine ⟨x, Hin.1, ?_⟩
      symm
      exact Hin.2

theorem prepostconditions_unwrap {ps : List (CoreLabel × Procedure.Check)} :
post ∈ List.map Procedure.Check.expr (ListMap.values ps) →
∃ label attr md, (label, { expr := post, attr := attr, md := md : Procedure.Check }) ∈ ps := by
  intros H
  induction ps
  case nil =>
    cases H
  case cons h t ih =>
    simp at H
    cases H with
    | intro c Hc
    simp [ListMap.values] at Hc
    cases Hc.1 with
    | inl Hin =>
      simp_all
      refine ⟨h.1, c.attr, c.md, ?_⟩
      left
      simp [← Hc, Hin]
    | inr Hin =>
      simp
      specialize ih ?_
      . simp [← Hc.2]
        refine ⟨c, ⟨Hin, rfl⟩⟩
      . cases ih with
        | intro label ih => grind

theorem updatedStateIsDefinedMono :
  (σ k').isSome = true →
  (updatedState σ k v k').isSome = true := by
  intros Hsome
  simp [updatedState]
  by_cases Heq : (k' = k) <;> simp [Heq]
  case neg => assumption

theorem EvalExpressionUpdatedState {δ : CoreEval}:
Imperative.WellFormedSemanticEvalVar δ →
Core.WellFormedCoreEvalCong δ →
Imperative.WellFormedSemanticEvalVal δ →
¬ k ∈ (Imperative.HasVarsPure.getVars e) →
δ σ e = some v' →
δ (updatedState σ k v) e = some v' := by
  intros Hwfv Hwfc Hwfvl Hnin Hsome
  simp [Imperative.WellFormedSemanticEvalVar, Imperative.HasFvar.getFvar] at Hwfv
  simp [Imperative.WellFormedSemanticEvalVal] at Hwfvl
  have Hval := Hwfvl.2
  simp [← Hsome] at *
  induction e <;> simp [Imperative.HasVarsPure.getVars, Lambda.LExpr.LExpr.getVars] at *
  case const c | op o ty | bvar b =>
    rw [Hval]; rw [Hval]; constructor; constructor
  case fvar m n ty =>
    simp [Hwfv]
    simp [updatedState]
    grind
  case abs m ty e ih =>
    apply ((Hwfc.1 (updatedState σ k v) σ))
    grind
  case quant m kk ty tr e trih eih =>
    apply Hwfc.quantcongr <;> grind
  case app m fn e fnih eih =>
    apply Hwfc.appcongr <;> grind
  case ite m c t e cih tih eih =>
    apply Hwfc.itecongr <;> grind
  case eq m e1 e2 e1ih e2ih =>
    apply Hwfc.eqcongr <;> grind

theorem EvalExpressionsUpdatedState {δ : CoreEval} :
  Imperative.WellFormedSemanticEvalVar δ →
  Core.WellFormedCoreEvalCong δ →
  Imperative.WellFormedSemanticEvalVal δ →
  ¬ k ∈ es.flatMap Imperative.HasVarsPure.getVars →
  EvalExpressions (P:=Core.Expression) δ σ es vs →
  EvalExpressions (P:=Core.Expression) δ (updatedState σ k v) es vs := by
  intros Hwfv Hwfc Hwfvl Hnin Heval
  have Hlen := EvalExpressionsLength Heval
  induction es generalizing vs σ
  case nil =>
    have Hnil := List.eq_nil_of_length_eq_zero (Eq.symm Hlen)
    simp [Hnil]
    constructor
  case cons h t ih =>
    cases vs
    . cases Heval
    . case cons h' t' =>
      rcases Heval
      case eval_some Hdef Heval Hevals =>
      constructor
      . exact updatedStateDefMonotone Hdef
      . apply EvalExpressionUpdatedState <;> simp_all
      . apply ih <;> simp_all

theorem EvalExpressionUpdatedStates {δ : CoreEval} :
  Imperative.WellFormedSemanticEvalVar δ →
  Core.WellFormedCoreEvalCong δ →
  Imperative.WellFormedSemanticEvalVal δ →
  ks'.length = vs'.length →
  ks'.Nodup →
  ks'.Disjoint (Imperative.HasVarsPure.getVars e) →
  δ σ e = some v →
  δ (updatedStates σ ks' vs') e = some v := by
  intros Hwfv Hwfc Hwfvl Hlen Hnd Hnin Heval
  induction ks' generalizing vs' σ
  case nil =>
    have Hnil := List.eq_nil_of_length_eq_zero (Eq.symm Hlen)
    simp [Hnil]
    simp [updatedStates, updatedStates']
    assumption
  case cons h t ih =>
    cases vs'
    . simp_all
    . case cons h' t' =>
      simp [updatedStates, updatedStates']
      rw [← updatedStateComm']
      apply EvalExpressionUpdatedState <;> try assumption
      . intros Hin
        apply Hnin _ Hin
        simp_all
      . apply ih <;> simp_all
        apply List.Disjoint.mono_left _ Hnin
        simp_all
      . rw [List.unzip_zip] <;> grind

theorem EvalExpressionsUpdatedStates {δ : CoreEval} :
  Imperative.WellFormedSemanticEvalVar δ →
  Core.WellFormedCoreEvalCong δ →
  Imperative.WellFormedSemanticEvalVal δ →
  ks'.length = vs'.length →
  ks'.Nodup →
  ks'.Disjoint (es.flatMap Imperative.HasVarsPure.getVars) →
  EvalExpressions (P:=Core.Expression) δ σ es vs →
  EvalExpressions (P:=Core.Expression) δ (updatedStates σ ks' vs') es vs := by
  intros Hwfv Hwfc Hwfvl Hlen Hnd Hnin Heval
  have Hlen := EvalExpressionsLength Heval
  induction ks' generalizing vs' σ
  case nil =>
    have Hnil := List.eq_nil_of_length_eq_zero (Eq.symm Hlen)
    simp [Hnil]
    simp [updatedStates, updatedStates']
    assumption
  case cons h t ih =>
    cases vs'
    . simp_all
    . case cons h' t' =>
      simp [updatedStates, updatedStates']
      rw [← updatedStateComm']
      apply EvalExpressionsUpdatedState <;> try assumption
      . intros Hin
        apply Hnin _ Hin
        simp_all
      . apply ih <;> simp_all
        apply List.Disjoint.mono_left _ Hnin
        simp_all
      . rw [List.unzip_zip] <;> grind

theorem ReadValueUpdatedState :
  x ≠ k →
  σ x = some v' →
  updatedState σ k v x = some v' := by
  intros Hne Hsome
  simp [updatedState, Hne] <;> simp_all

theorem ReadValueUpdatedStates :
  ¬ x ∈ ks →
  σ x = some v' →
  ks.length = vs.length →
  updatedStates σ ks vs x = some v' := by
  intros Hne Hsome Hlen
  induction ks generalizing σ vs
  case nil =>
    cases vs <;> simp_all
    simp [updatedStates, updatedStates']
    assumption
  case cons h t ih =>
    cases vs
    case nil => simp_all
    case cons h' t' =>
    simp [updatedStates, updatedStates']
    apply ih <;> simp_all
    simp [updatedState]
    simp_all

theorem ReadValuesUpdatedState :
  ¬ k ∈ ks →
  ReadValues σ ks vs →
  ReadValues (updatedState σ k v) ks vs := by
  intros Hin Hrd
  induction Hrd
  case read_none =>
    apply ReadValues.read_none
  case read_some xs' vs' x v' Hsome Hrd Hrd2 =>
    constructor <;> try assumption
    apply ReadValueUpdatedState <;> simp_all
    apply Ne.symm Hin.1
    apply Hrd2 <;> simp_all

theorem ReadValuesUpdatedStates :
  ks'.length = vs'.length →
  ks'.Disjoint ks →
  ReadValues σ ks vs →
  ReadValues (updatedStates σ ks' vs') ks vs := by
  intros Hlen Hin Hrd
  induction ks generalizing vs
  case nil =>
    cases Hrd
    constructor
  case cons h t ih =>
    cases vs with
    | nil =>
      cases Hrd
    | cons h t =>
      cases Hrd with
      | read_some Hh Ht =>
      constructor
      . refine ReadValueUpdatedStates ?_ Hh Hlen
        . intros Hin'
          exact Hin Hin' List.mem_cons_self
      . apply ih ?_ Ht
        apply List.Disjoint.mono_right _ Hin
        simp_all

theorem ReadValueUpdatedState' :
  x ≠ k →
  updatedState σ k v x = some v' →
  σ x = some v' := by
  intros Hne Hsome
  simp [updatedState, Hne] at Hsome <;> simp_all

theorem ReadValueUpdatedStates' :
  ¬ x ∈ ks →
  updatedStates σ ks vs x = some v' →
  ks.length = vs.length →
  σ x = some v' := by
  intros Hne Hsome Hlen
  induction ks generalizing σ vs
  case nil =>
    cases vs <;> simp_all
    simp [updatedStates, updatedStates'] at Hsome
    assumption
  case cons h t ih =>
    cases vs
    case nil => simp_all
    case cons h' t' =>
    simp [updatedStates, updatedStates'] at *
    specialize ih Hne.2 Hsome Hlen
    exact ReadValueUpdatedState' Hne.1 ih

theorem ReadValuesUpdatedState' :
  ¬ k ∈ ks →
  ReadValues (updatedState σ k v) ks vs →
  ReadValues σ ks vs := by
  intros Hin Hrd
  induction Hrd
  case read_none =>
    apply ReadValues.read_none
  case read_some xs' vs' x v' Hsome Hrd Hrd2 =>
    constructor <;> try assumption
    apply ReadValueUpdatedState' (k:=k) (v:=v) _ Hsome
    . exact Ne.symm (List.ne_of_not_mem_cons Hin)
    . apply Hrd2
      exact List.not_mem_of_not_mem_cons Hin

theorem ReadValuesUpdatedStates' :
  ks'.length = vs'.length →
  ks'.Disjoint ks →
  ReadValues (updatedStates σ ks' vs') ks vs →
  ReadValues σ ks vs := by
  intros Hlen Hin Hrd
  induction ks generalizing vs
  case nil =>
    cases Hrd
    constructor
  case cons h t ih =>
    cases vs with
    | nil =>
      cases Hrd
    | cons h t =>
      cases Hrd with
      | read_some Hh Ht =>
      constructor
      . refine ReadValueUpdatedStates' ?_ Hh Hlen
        . intros Hin'
          exact Hin Hin' List.mem_cons_self
      . apply ih ?_ Ht
        apply List.Disjoint.mono_right _ Hin
        simp_all

theorem ReadValuesUpdatedStatesSame :
  ks.length = vs.length →
  ks.Nodup →
  ReadValues (updatedStates σ ks vs) ks vs := by
  intros Hlen Hnd
  induction ks generalizing σ vs
  case nil =>
    cases vs <;> simp_all
    constructor
  case cons h t ih =>
    cases vs
    . simp_all
    . simp [updatedStates, updatedStates']
      rw [← updatedStateComm']
      constructor <;> simp_all
      simp [updatedState]
      rw [updatedStateComm']
      apply ih <;> simp_all
      rw [List.unzip_zip] <;> simp_all
      rw [List.unzip_zip] <;> simp_all

theorem EvalStatementContractInitVar :
  Imperative.WellFormedSemanticEvalVar δ →
  σ v = some vv →
  σ v' = none →
  EvalStatementContract π δ σ
    (createInitVar ((v', ty), v))
    (updatedState σ v' vv) := by
  intros Hwf Hsome Hnone
  simp [createInitVar]
  constructor
  constructor
  . apply Imperative.EvalCmd.eval_init <;> try assumption
    have Hwfv := Hwf (Lambda.LExpr.fvar () v none) v σ
    rw [Hwfv]; assumption
    simp [Imperative.HasFvar.getFvar]
    apply Imperative.InitState.init Hnone
    simp [updatedState]
    intros y Hne
    simp [updatedState]
    intros Heq
    simp_all
  . simp [Imperative.isDefinedOver,
          Imperative.isDefined,
          Imperative.HasVarsImp.modifiedVars,
          Command.modifiedVars,
          Imperative.Cmd.modifiedVars]

theorem EvalStatementsContractInitVars :
  Imperative.WellFormedSemanticEvalVar δ →
  -- the generated old variable names shouldn't overlap with original variables
  List.Nodup ((trips.unzip.fst.unzip.fst) ++ (trips.unzip.snd)) →
  ReadValues σ (trips.unzip.snd) vvs →
  Imperative.isNotDefined σ (trips.unzip.fst.unzip.fst) →
  EvalStatementsContract π δ σ
    (createInitVars trips)
    (updatedStates σ
      (trips.unzip.fst.unzip.fst) vvs) := by
  intros Hwf Hndup Hdef Hndef
  induction trips generalizing σ vvs with
  | nil =>
    simp [createInitVars, updatedStates]
    constructor
  | cons h t ih =>
    cases Hdef
    next vs vv Hsome Hrest =>
    cases h with
    | mk pair v =>
    cases pair with
    | mk v' ty =>
    apply Imperative.EvalBlock.stmts_some_sem
    apply EvalStatementContractInitVar <;> try assumption
    apply Hndef <;> simp_all
    unfold updatedStates
    apply ih
    . simp_all
      have HH := Hndup.2
      apply List.Sublist.nodup (by simp) HH
    . refine ReadValuesUpdatedState ?_ Hrest
      simp [List.Nodup] at Hndup
      have Hin := Hndup.1
      apply List.forall_mem_ne.mp
      intros x' ty'
      simp_all
    . simp [Imperative.isNotDefined] at Hndef ⊢
      intros v x x1 Hin
      simp [updatedState]
      split <;> simp_all
      apply Hndef.2
      apply Hin

theorem EvalStatementContractInit :
  Imperative.WellFormedSemanticEvalVar δ →
  δ σ e = some vv →
  σ v' = none →
  EvalStatementContract π δ σ
    (createInit ((v', ty), e))
    (updatedState σ v' vv) := by
  intros Hwf Hsome Hnone
  simp [createInit]
  constructor
  constructor
  . apply Imperative.EvalCmd.eval_init <;> try assumption
    apply Imperative.InitState.init Hnone
    simp [updatedState]
    intros y Hne
    simp [updatedState]
    intros Heq
    simp_all
  . simp [Imperative.isDefinedOver,
          Imperative.isDefined,
          Command.modifiedVars,
          Imperative.Cmd.modifiedVars,
          Imperative.HasVarsImp.modifiedVars]

theorem EvalStatementsContractInits :
  Imperative.WellFormedSemanticEvalVar δ →
  Imperative.WellFormedSemanticEvalVal δ →
  WellFormedCoreEvalCong δ →
  -- the generated old variable names shouldn't overlap with original variables
  trips.unzip.1.unzip.1.Disjoint (List.flatMap (Imperative.HasVarsPure.getVars (P:=Expression)) trips.unzip.2) →
  List.Nodup (trips.unzip.1.unzip.1) →
  EvalExpressions (P:=Core.Expression) δ σ (trips.unzip.2) vvs →
  -- ReadValues σ (trips.unzip.2) vvs →
  Imperative.isNotDefined σ (trips.unzip.1.unzip.1) →
  EvalStatementsContract π δ σ
    (createInits trips)
    (updatedStates σ
      (trips.unzip.1.unzip.1) vvs) := by
  intros Hwfvr Hwfvl Hwfc Hdisj Hndup Hdef Hndef
  induction trips generalizing σ vvs with
  | nil =>
    simp [createInits, updatedStates]
    constructor
  | cons h t ih =>
    cases Hdef
    next vs vv Hsome Hrest =>
    cases h with
    | mk pair v =>
    cases pair with
    | mk v' ty =>
    apply Imperative.EvalBlock.stmts_some_sem
    apply EvalStatementContractInit <;> try assumption
    apply Hndef <;> simp_all
    unfold updatedStates
    apply ih
    . apply List.Disjoint.mono ?_ ?_ Hdisj <;> simp_all
    . simp_all
    . refine EvalExpressionsUpdatedState Hwfvr Hwfc Hwfvl ?_ Hrest
      simp at Hdisj
      have Hdisj' :
        [v'].Disjoint (List.flatMap Imperative.HasVarsPure.getVars t.unzip.snd) := by
        apply List.Disjoint.mono ?_ ?_ Hdisj <;> simp_all
      intros Hin
      exact Hdisj' (List.mem_singleton.mpr rfl) Hin
    . simp [Imperative.isNotDefined] at Hndef ⊢
      intros v x x1 Hin
      simp [updatedState]
      split <;> simp_all
      apply Hndef.2
      apply Hin

theorem EvalStatementContractHavocUpdated :
  ∀ vv,
  Imperative.WellFormedSemanticEvalVar δ →
  σ v = some vv' →
  EvalStatementContract π δ σ
    (createHavoc v)
    (updatedState σ v vv) := by
  intros vv Hwf Hsome
  simp [createHavoc]
  constructor
  constructor
  . constructor
    . exact updatedStateUpdate Hsome
    . assumption
  . simp [Imperative.isDefinedOver, Imperative.isDefined,
          Imperative.HasVarsImp.modifiedVars,
          Command.modifiedVars,
          Imperative.Cmd.modifiedVars, Option.isSome]
    split <;> simp_all

theorem ReadValuesSome :
  Imperative.isDefined σ ks →
  ∃ vs, ReadValues σ ks vs := by
  intros H
  induction ks
  case nil =>
    exists []
    constructor
  case cons h t ih =>
    have Hh := H h
    have Ht := ih ?_
    . cases Ht with
      | intro t' Hrd =>
      simp [Option.isSome] at Hh
      split at Hh <;> simp_all
      next x val heq =>
      exists val :: t'
      constructor <;> simp_all
    . simp [Imperative.isDefined] at *
      intros v a
      simp_all

theorem idents2havocsApp :
createHavocs (vs₁ ++ vs₂) =
createHavocs vs₁ ++ createHavocs vs₂ := by
cases vs₁ <;> simp [createHavocs]

theorem createFvarsSubstStores :
  ks1.length = ks2.length →
  Imperative.WellFormedSemanticEvalVar δ →
  Imperative.substDefined σ σA (ks1.zip ks2) →
  Imperative.substStores σ σA (ks1.zip ks2) →
  ReadValues σA ks2 argVals →
  EvalExpressions (P:=Core.Expression) δ σ (createFvars ks1) argVals := by
    intros Hlen Hwfv Hdef Hsubst Hrd
    simp [createFvars]
    have Hlen2 := ReadValuesLength Hrd
    induction Hrd generalizing ks1
    case read_none => simp_all; constructor
    case read_some xs vs x v Hsome Hrds ih' =>
      induction ks1 generalizing ks2 vs v with
      | nil =>
        simp_all
      | cons h t ih =>
        simp
        constructor
        . simp [createFvar,
                Imperative.HasVarsPure.getVars,
                Lambda.LExpr.LExpr.getVars]
          simp [Imperative.substDefined] at Hdef
          intros hh Hin
          apply (Hdef hh x ?_).1
          left
          simp_all
        . simp [createFvar]
          simp [Imperative.WellFormedSemanticEvalVar] at Hwfv
          simp [Imperative.HasFvar.getFvar] at Hwfv
          simp [Hwfv]
          rw [Hsubst]
          exact Hsome
          simp_all
        . apply ih' <;> simp_all
          . intros k1 k2 Hin
            apply Hdef <;> simp_all
          . simp [Imperative.substStores] at *
            intros
            apply Hsubst <;> simp_all

theorem EvalStatementsContractHavocVars :
  Imperative.WellFormedSemanticEvalVar δ →
  Imperative.isDefined σ vs →
  HavocVars σ vs σ' →
  EvalStatementsContract π δ σ
    (createHavocs vs) σ' := by
  intros Hwfv Hdef Hhav
  simp [createHavocs]
  induction vs generalizing σ
  case nil =>
    have Heq := HavocVarsEmpty Hhav
    simp_all
    exact Imperative.EvalBlock.stmts_none_sem
  case cons h t ih =>
    simp [createHavoc]
    cases Hhav with
    | update_some Hup Hhav =>
    apply Imperative.EvalBlock.stmts_some_sem
    apply EvalStmtRefinesContract
    apply Imperative.EvalStmt.cmd_sem
    apply EvalCommand.cmd_sem
    apply Imperative.EvalCmd.eval_havoc <;> try assumption
    . simp [Imperative.isDefinedOver, Command.modifiedVars,Imperative.Cmd.modifiedVars,
            Imperative.HasVarsImp.modifiedVars]
      simp [Imperative.isDefined] at Hdef ⊢
      apply Hdef.1
    . apply ih <;> try assumption
      . apply UpdateStateDefMonotone (σ:=σ) (vs:=t) <;> try assumption
        simp [Imperative.isDefined] at * <;> simp_all

theorem updatedStateInv :
¬k = h →
updatedState σ h h' k = σ k := by
intros Hne
unfold updatedState
simp [Hne] <;> simp_all

theorem updatedStatesInv :
¬k ∈ ks' →
updatedStates σ ks' vs' k = σ k := by
intros Hin
induction ks' generalizing vs' σ <;> simp_all
case nil =>
  simp [updatedStates, updatedStates']
case cons h t ih =>
  cases vs'
  case nil =>
    simp [updatedStates, updatedStates']
  case cons h' t' =>
    unfold updatedStates
    have Hsome' : (updatedState σ h h') k = σ k := by
      apply updatedStateInv <;> simp_all
    simp [← Hsome']
    exact ih

theorem UpdateStateUpdatedDists
{P : Imperative.PureExpr}
{σ σ' : Imperative.SemanticStore P}
{h : P.Ident} {v : P.Expr} {ks : List P.Ident} {vs : List P.Expr} :
¬ h ∈ ks →
Imperative.UpdateState P σ h v σ' →
Imperative.UpdateState P (updatedStates σ ks vs) h v (updatedStates σ' ks vs) := by
intros Hnin Hup
cases Hup with
| update Hsome HH =>
simp [updatedStates]
generalize Hls : ks.zip vs = ls
induction ls generalizing ks vs σ σ'
case nil =>
  simp [updatedStates']
  simp_all
  constructor <;> try simp_all
  rfl
case cons h t ih H' =>
  simp [updatedStates']
  have Hzip := List.zip_eq_cons_iff.mp Hls
  cases Hzip with | intro l1 Hzip => cases Hzip with | intro l2 Hzip =>
  apply ih ?_ (ks:=l1) (vs:=l2) <;> simp_all
  . simp [updatedState]
    split <;> simp_all
  . intros y Hne
    simp [updatedState]
    split <;> simp_all
  . cases h with
    | mk l r =>
    simp [updatedState] at *
    split <;> simp_all

theorem InitStateUpdatedDists
{P : Imperative.PureExpr}
{σ σ' : Imperative.SemanticStore P}
{h : P.Ident} {v : P.Expr} {ks : List P.Ident} {vs : List P.Expr} :
¬ h ∈ ks →
Imperative.InitState P σ h v σ' →
Imperative.InitState P (updatedStates σ ks vs) h v (updatedStates σ' ks vs) := by
intros Hnin Hup
cases Hup with
| init Hsome HH =>
simp [updatedStates]
generalize Hls : ks.zip vs = ls
induction ls generalizing ks vs σ σ'
case nil =>
  simp [updatedStates']
  simp_all
  constructor <;> try simp_all
case cons h t ih H' =>
  simp [updatedStates']
  have Hzip := List.zip_eq_cons_iff.mp Hls
  cases Hzip with | intro l1 Hzip => cases Hzip with | intro l2 Hzip =>
  apply ih ?_ (ks:=l1) (vs:=l2) <;> simp_all
  . simp [updatedState]
    split <;> simp_all
  . intros y Hne
    simp [updatedState]
    split <;> simp_all
  . cases h with
    | mk l r =>
    simp [updatedState] at *
    split <;> simp_all

theorem UpdateStatesUpdatedDists
{P : Imperative.PureExpr}
{σ σ' : Imperative.SemanticStore P}
{ks ks': List P.Ident} {vs vs' : List P.Expr} :
  ks.Disjoint ks' →
  UpdateStates σ ks vs σ' →
  UpdateStates (updatedStates σ ks' vs') ks vs (updatedStates σ' ks' vs') := by
intros Hnd Hup
induction Hup
case update_none =>
  exact UpdateStates.update_none
case update_some Hup Hups ih =>
  apply UpdateStates.update_some
  . apply UpdateStateUpdatedDists <;> try assumption
    simp [List.Disjoint] at Hnd
    simp_all
  . apply ih
    simp [List.Disjoint] at *
    simp_all

theorem InitStatesUpdatedDists
{P : Imperative.PureExpr}
{σ σ' : Imperative.SemanticStore P}
{ks ks': List P.Ident} {vs vs' : List P.Expr} :
  ks.Disjoint ks' →
  InitStates σ ks vs σ' →
  InitStates (updatedStates σ ks' vs') ks vs (updatedStates σ' ks' vs') := by
intros Hnd Hup
induction Hup
case init_none =>
  exact InitStates.init_none
case init_some Hup Hups ih =>
  apply InitStates.init_some
  . apply InitStateUpdatedDists <;> try assumption
    simp [List.Disjoint] at Hnd
    simp_all
  . apply ih
    simp [List.Disjoint] at *
    simp_all

theorem UpdateStatesUpdatedDist
{P : Imperative.PureExpr}
{σ σ' : Imperative.SemanticStore P}
{ks : List P.Ident} {vs : List P.Expr}
{k : P.Ident} {v : P.Expr} :
  ¬ k ∈ ks →
  UpdateStates σ ks vs σ' →
  UpdateStates (updatedState σ k v) ks vs (updatedState σ' k v) := by
intros Hnd Hup
have Hnd : ks.Disjoint [k] := by
  intros a Hin1 Hin2
  apply Hnd
  simp_all
have HH := UpdateStatesUpdatedDists (vs':=[v]) Hnd Hup
simp [updatedStates, updatedStates'] at HH
assumption

theorem HavocVarsUpdatedDists :
ks.Disjoint ks' →
HavocVars σ ks σ' →
HavocVars (updatedStates σ ks' vs') ks
          (updatedStates σ' ks' vs') := by
intros Hnd Hhav
induction ks generalizing σ
case nil =>
  have Heq := HavocVarsEmpty Hhav
  simp_all
  exact HavocVars.update_none
case cons h t ih =>
  cases Hhav
  next v σ'' Hup Hhav2 =>
  apply HavocVars.update_some (v:=v) (σ':=(updatedStates σ'' ks' vs'))
  . simp [List.Disjoint] at Hnd
    apply UpdateStateUpdatedDists Hnd.1 Hup
  . apply ih ?_ Hhav2
    apply List.Disjoint.mono_left ?_ Hnd
    simp_all

theorem InitVarsUpdatedDists :
ks.Disjoint ks' →
InitVars σ ks σ' →
InitVars (updatedStates σ ks' vs') ks
          (updatedStates σ' ks' vs') := by
intros Hnd Hhav
induction ks generalizing σ
case nil =>
  have Heq := InitVarsEmpty Hhav
  simp_all
  exact InitVars.init_none
case cons h t ih =>
  cases Hhav
  next v σ'' Hup Hhav2 =>
  apply InitVars.init_some (v:=v) (σ':=(updatedStates σ'' ks' vs'))
  . simp [List.Disjoint] at Hnd
    apply InitStateUpdatedDists Hnd.1 Hup
  . apply ih ?_ Hhav2
    apply List.Disjoint.mono_left ?_ Hnd
    simp_all

theorem HavocVarsUpdatedDist :
¬ k ∈ ks →
HavocVars σ ks σ' →
HavocVars (updatedState σ k v) ks
          (updatedState σ' k v) := by
intros Hnd Hhav
have Hnd : ks.Disjoint [k] := by
  intros a Hin1 Hin2
  apply Hnd
  simp_all
have HH := HavocVarsUpdatedDists (vs':=[v]) Hnd Hhav
simp [updatedStates, updatedStates'] at HH
assumption

theorem InitVarsUpdatedDist :
¬ k ∈ ks →
InitVars σ ks σ' →
InitVars (updatedState σ k v) ks
          (updatedState σ' k v) := by
intros Hnd Hhav
have Hnd : ks.Disjoint [k] := by
  intros a Hin1 Hin2
  apply Hnd
  simp_all
have HH := InitVarsUpdatedDists (vs':=[v]) Hnd Hhav
simp [updatedStates, updatedStates'] at HH
assumption

theorem UpdatedStatesDisjNotDefMonotone :
  ks.Disjoint ks' →
  ks.length = vs.length →
  Imperative.isNotDefined σ ks' →
  Imperative.isNotDefined (updatedStates σ ks vs) ks' := by
intros Hdis Hlen Hndef
simp [Imperative.isNotDefined, updatedStates] at *
intros v Hin
induction ks generalizing vs σ <;> simp_all
case nil =>
  simp [updatedStates']
  exact Hndef v Hin
case cons h t ih =>
  induction vs generalizing h t σ <;> simp_all
  case cons h' t' ih' =>
    simp [updatedStates']
    rw [ih] <;> try simp_all
    . apply List.Disjoint.mono_left _ Hdis
      simp_all
    . intros v Hin
      simp [updatedState]
      split <;> simp_all
      apply Hdis _ Hin
      simp_all

/-- We can't use arbitrary expressions for substitution,
    because then we can't say anything about the stores
    due to not knowing the exact form of the expressions -/
theorem Lambda.LExpr.substFvarCorrect :
  Core.WellFormedCoreEvalCong δ →
  Imperative.WellFormedSemanticEvalVar (P:=Expression) δ →
  Imperative.WellFormedSemanticEvalVal (P:=Expression) δ →
  Imperative.substStores σ σ' [(fro, to)] →
  -- NOTE: `to` shouldn't be referred to in the original expression as well, but it is not needed in this lemma.
  Imperative.invStores σ σ'
    ((@Imperative.HasVarsPure.getVars Expression _ _ e).removeAll [fro]) →
  -- NOTE: the old store is irrelevant because we assume congruence on old expressions as well,
  -- More relation between the old store would be needed if we remove old expression congruence from WellFormedSemanticEvalVal
  δ σ e = δ σ' (e.substFvar fro (createFvar to)) := by
  intros Hwfc Hwfvr Hwfvl Hsubst2 Hinv
  induction e <;> simp [Lambda.LExpr.substFvar, createFvar] at *
  case const c | op o ty | bvar x =>
    rw [Hwfvl.2]
    rw [Hwfvl.2]
    constructor
    constructor
  case fvar name ty =>
    simp [Imperative.WellFormedSemanticEvalVar] at Hwfvr
    split <;> try simp_all
    . simp [Imperative.substStores] at Hsubst2
      rw [Hwfvr]
      rw [Hwfvr]
      exact Hsubst2
      simp [Imperative.HasFvar.getFvar]
      simp [Imperative.HasFvar.getFvar]
    . next Hne =>
      simp [Imperative.invStores, Imperative.substStores,
            Imperative.HasVarsPure.getVars,
            Lambda.LExpr.LExpr.getVars, List.removeAll, Hne] at Hinv
      rw [Hwfvr]
      rw [Hwfvr]
      exact Hinv
      simp [Imperative.HasFvar.getFvar]
      simp [Imperative.HasFvar.getFvar]
  case abs m ty e ih  =>
    specialize ih Hinv
    have e2 := (e.substFvar fro (Lambda.LExpr.fvar () to none))
    have Hwfc := Hwfc.1 σ σ' e ((e.substFvar fro (Lambda.LExpr.fvar () to none)))
    grind
  case quant m k ty tr e trih eih =>
    simp [Imperative.invStores, Imperative.substStores,
          Imperative.HasVarsPure.getVars, Lambda.LExpr.LExpr.getVars] at *
    simp [List.app_removeAll, List.zip_append] at *
    specialize eih ?_
    · intros k1 k2 Hin
      rw [Hinv]
      right;
      assumption
    specialize trih ?_
    · intros k1 k2 Hin
      rw [Hinv]
      left;
      assumption
    apply Hwfc.quantcongr <;> grind
  case app m c fn fih eih =>
    simp [Imperative.invStores, Imperative.substStores,
          Imperative.HasVarsPure.getVars, Lambda.LExpr.LExpr.getVars] at *
    simp [List.app_removeAll, List.zip_append] at *
    specialize fih ?_
    . intros k1 k2 Hin
      rw [Hinv]
      left; assumption
    specialize eih ?_
    . intros k1 k2 Hin
      rw [Hinv]
      right; assumption
    apply Hwfc.appcongr <;> grind
  case ite m c t e cih tih eih =>
    simp [Imperative.invStores, Imperative.substStores,
          Imperative.HasVarsPure.getVars, Lambda.LExpr.LExpr.getVars] at *
    simp [List.app_removeAll, List.zip_append] at *
    specialize cih ?_
    . intros k1 k2 Hin
      rw [Hinv]
      left; assumption
    specialize tih ?_
    . intros k1 k2 Hin
      rw [Hinv]
      right; left; assumption
    specialize eih ?_
    . intros k1 k2 Hin
      rw [Hinv]
      right; right; assumption
    apply Hwfc.itecongr <;> grind
  case eq m e1 e2 e1ih e2ih =>
    simp [Imperative.invStores, Imperative.substStores,
          Imperative.HasVarsPure.getVars, Lambda.LExpr.LExpr.getVars] at *
    simp [List.app_removeAll, List.zip_append] at *
    specialize e1ih ?_
    . intros k1 k2 Hin
      rw [Hinv]
      left; assumption
    specialize e2ih ?_
    . intros k1 k2 Hin
      rw [Hinv]
      right; assumption
    apply Hwfc.eqcongr <;> grind

theorem Lambda.LExpr.substFvarsCorrectZero :
  Core.WellFormedCoreEvalCong δ →
  Imperative.WellFormedSemanticEvalVar δ →
  Imperative.WellFormedSemanticEvalVal δ →
  Imperative.invStores σ σ' (Imperative.HasVarsPure.getVars e) →
  δ σ e = δ σ' e := by
  intros Hwfc Hwfvr Hwfvl Hinv
  induction e <;> simp at *
  case const c | op o ty | bvar x =>
    rw [Hwfvl.2]
    rw [Hwfvl.2]
    constructor
    constructor
  case fvar m name ty =>
    simp [Imperative.WellFormedSemanticEvalVar] at Hwfvr
    specialize Hwfvr (Lambda.LExpr.fvar m name ty) name
    rw [Hwfvr]
    rw [Hwfvr]
    rw [Hinv]
    simp [Imperative.HasVarsPure.getVars, Lambda.LExpr.LExpr.getVars]
    simp [Imperative.HasFvar.getFvar]
    simp [Imperative.HasFvar.getFvar]
  case abs m ty e ih  =>
    specialize ih Hinv
    have Hwfc := Hwfc.abscongr σ σ' e e ih
    apply Hwfc
  case quant m k ty tr e trih eih =>
    simp [Imperative.invStores, Imperative.substStores,
          Imperative.HasVarsPure.getVars, Lambda.LExpr.LExpr.getVars] at *
    simp [List.zip_append] at *
    specialize trih ?_
    . intros k1 k2 Hin
      rw [Hinv]
      left; assumption
    specialize eih ?_
    . intros k1 k2 Hin
      rw [Hinv]
      right; assumption
    apply Hwfc.quantcongr <;> grind
  case app m fn e fih eih =>
    simp [Imperative.invStores, Imperative.substStores,
          Imperative.HasVarsPure.getVars, Lambda.LExpr.LExpr.getVars] at *
    simp [List.zip_append] at *
    specialize fih ?_
    . intros k1 k2 Hin
      rw [Hinv]
      left; assumption
    specialize eih ?_
    . intros k1 k2 Hin
      rw [Hinv]
      right; assumption
    apply Hwfc.appcongr <;> grind
  case ite m c t e cih tih eih =>
    simp [Imperative.invStores, Imperative.substStores,
          Imperative.HasVarsPure.getVars, Lambda.LExpr.LExpr.getVars] at *
    simp [List.zip_append] at *
    specialize cih ?_
    . intros k1 k2 Hin
      rw [Hinv]
      left; assumption
    specialize tih ?_
    . intros k1 k2 Hin
      rw [Hinv]
      right; left; assumption
    specialize eih ?_
    . intros k1 k2 Hin
      rw [Hinv]
      right; right; assumption
    apply Hwfc.itecongr <;> grind
  case eq m e1 e2 e1ih e2ih =>
    simp [Imperative.invStores, Imperative.substStores,
          Imperative.HasVarsPure.getVars, Lambda.LExpr.LExpr.getVars] at *
    simp [List.zip_append] at *
    specialize e1ih ?_
    . intros k1 k2 Hin
      rw [Hinv]
      left; assumption
    specialize e2ih ?_
    . intros k1 k2 Hin
      rw [Hinv]
      right; assumption
    apply Hwfc.eqcongr <;> grind

theorem updatedStoresInvStores :
  ¬ k ∈ ks →
  Imperative.invStores σ (updatedState σ k v) ks := by
  intros Hnin k1 k2 Hin
  have Heq : k1 = k2 := zip_self_eq Hin
  simp_all
  have Hin := (List.of_mem_zip Hin).1
  have Hne : k2 ≠ k := by
    exact ne_of_mem_of_not_mem Hin Hnin
  simp [updatedState]
  simp_all

theorem invStoresSubstHead :
  Imperative.substStores (P := Expression) σ (updatedState σ h' v₁) [(h, h')] →
  ¬ h' ∈ vs →
  Imperative.invStores σ (updatedState σ h' v₁) (List.removeAll vs [h]) := by
intros Hnin Hsubst k1 k2
apply updatedStoresInvStores
simp [List.removeAll]
simp_all

theorem invStoresEraseDups' :
  Imperative.invStores (P:=Expression) σ σ' vs.eraseDups →
  Imperative.invStores (P:=Expression) σ σ' vs := by
  intros Hinv k1 k2 Hin
  specialize Hinv k1 k2
  have Heq := zip_self_eq Hin
  simp_all
  apply Hinv
  apply zip_self_eq'
  refine eraseDupsBy.sound ?_
  have Hsub := eraseDupsBy.sound Hin
  have Hmem := List.of_mem_zip Hin
  exact Hmem.1

theorem invStoresSubstTail'  [BEq P.Ident] [LawfulBEq P.Ident] {σ : Imperative.SemanticStore P}:
  σ h = some v₁ →
  Imperative.invStores (P:=P) σ₀ σ (List.removeAll vs (h :: t)) →
  Imperative.invStores (updatedState σ₀ h v₁) σ (List.removeAll vs t) := by
  intros Hsome Hinv k1 k2 Hin
  have Heq := zip_self_eq Hin
  simp_all
  simp [Imperative.invStores, Imperative.substStores] at *
  simp [updatedState]
  split <;> simp_all
  . next neq =>
    apply Hinv
    apply zip_self_eq'
    have Hin := (List.of_mem_zip Hin).1
    apply removeAll_cons <;> simp_all

theorem invStoresSubstTail :
  Imperative.substStores (P := Expression) σ σ' ((h, h') :: t.zip t') →
  Imperative.substStores (P := Expression) (updatedState σ h' v₁) σ' (t.zip t') →
  σ h = some v₁ →
  h ≠ h' →
  Imperative.invStores σ σ' (List.removeAll vs ((h :: t) ++ (h' :: t'))) →
  Imperative.invStores (updatedState σ h' v₁) σ'
                            (List.removeAll (vs.replaceAll h h') (t ++ t')) := by
  intros Hsubst1 Hsubst2 Hsome Hne Hinv k1 k2 Hin
  have Heq := zip_self_eq Hin
  simp_all
  simp [Imperative.invStores, Imperative.substStores] at *
  simp [updatedState]
  split
  . rw [← Hsubst1 h] <;> simp_all
  . next neq =>
    apply Hinv
    apply zip_self_eq'
    have Hin := (List.of_mem_zip Hin).1
    have Hsub := removeAll_sublist (vs.replaceAll h h') (t ++ t')
    have Hin' : k2 ∈ (vs.replaceAll h h') := List.Sublist.mem Hin Hsub
    have Hor := in_replaceAll_removeAll Hin
    cases Hor <;> simp_all
    apply removeAll_cons
    . intros Heq
      simp_all
      have Hnmem : ¬ h ∈ vs.replaceAll h h' := replaceAll_not_mem Hne
      exact Hnmem Hin'
    . simp [List.removeAll] at *
      simp_all

theorem subst_create_replace :
(Imperative.HasVarsPure.getVars (Lambda.LExpr.substFvar e h (createFvar h'))) =
(Imperative.HasVarsPure.getVars e).replaceAll h h'
:= by
induction e <;> simp [
    Imperative.HasVarsPure.getVars,
    Lambda.LExpr.LExpr.getVars,
    Lambda.LExpr.substFvar,
    createFvar,
    List.replaceAll,
  ] at * <;> try assumption
case fvar name ty =>
  split <;> try simp_all
  simp [Lambda.LExpr.LExpr.getVars]
  split <;> simp_all
  simp [Lambda.LExpr.LExpr.getVars]
case app fn e fn_ih e_ih =>
  rw [fn_ih, e_ih]
  rw [List.replaceAll_app]
case quant k ty tr_ih e_ih =>
  rw [tr_ih, e_ih]
  rw [List.replaceAll_app]
case ite c t e c_ih t_ih e_ih =>
  rw [c_ih, t_ih, e_ih]
  rw [List.replaceAll_app]
  rw [List.replaceAll_app]
case eq e1 e2 e1_ih e2_ih =>
  rw [e1_ih, e2_ih]
  rw [List.replaceAll_app]

theorem substDefined_tail :
Imperative.substDefined σ σ' (h :: t) →
Imperative.substDefined σ σ' t := by
intros Hsubst k1 k2 Hin
apply Hsubst
exact List.mem_cons_of_mem h Hin

theorem substNodup_tail :
Imperative.substNodup (h :: t) →
Imperative.substNodup t := by
intros Hsubst
simp [Imperative.substNodup] at *
exact (List.nodup_cons.mp (nodup_middle Hsubst.right)).right

theorem substDefined_updatedState :
Imperative.substDefined σ σ' ls →
Imperative.substDefined (updatedState σ k v) σ' ls := by
intros Hsubst k1 k2 Hin
apply And.intro
. apply updatedStateIsDefinedMono
  exact (Hsubst k1 k2 Hin).1
. exact (Hsubst k1 k2 Hin).2

theorem zip_notin_fst :
  t.length = t'.length →
  (∀ x, ¬(h, x) ∈ List.zip t t') →
  ¬ h ∈ t := by
intros Hlen H
induction t generalizing t' h <;> simp_all
case cons h t ih =>
induction t' <;> simp_all
case cons h' t' =>
have HH := H h'
simp_all
exact ih rfl H

theorem zip_notin_snd :
  t.length = t'.length →
  (∀ x, ¬(x, h) ∈ List.zip t t') →
  ¬ h ∈ t' := by
intros Hlen H
induction t' generalizing t h <;> simp_all
case cons h t ih =>
induction t <;> simp_all
case cons h' t' =>
have HH := H h'
simp_all
exact ih Hlen H

theorem substNodup_ht :
  t.length = t'.length →
  Imperative.substNodup ((h, h') :: List.zip t t') →
  ¬ h ∈ t ∧ ¬ h' ∈ t' := by
  intros Hlen Hsubst
  simp [Imperative.substNodup] at Hsubst
  apply And.intro
  . intros Hin
    exact zip_notin_fst Hlen Hsubst.1.1 Hin
  . have Hnd := nodup_middle Hsubst.2
    simp at Hnd
    have Hnd' := Hnd.1.2
    exact zip_notin_snd Hlen Hnd'

theorem getVarsSubstCreateFvar :
v ∈ (Imperative.HasVarsPure.getVars (P:=Expression) (Lambda.LExpr.substFvar e h (createFvar h'))) →
v ∈ (Imperative.HasVarsPure.getVars e) ∨ v = h' := by
intros Hin
induction e <;>
simp [Lambda.LExpr.substFvar,
      Imperative.HasVarsPure.getVars,
      Lambda.LExpr.LExpr.getVars,
      createFvar
      ] at * <;> try simp_all
case fvar name ty =>
  split at Hin <;> simp [Lambda.LExpr.LExpr.getVars] at * <;> simp_all
case app fn e fn_ih e_ih =>
  cases Hin <;> simp_all
  cases fn_ih <;> simp_all
  cases e_ih <;> simp_all
case quant k ty tr_ih e_ih =>
  cases Hin <;> simp_all
  cases tr_ih <;> simp_all
  cases e_ih <;> simp_all
case ite c t e c_ih t_ih e_ih =>
  cases Hin with
  | inl Hin => cases (c_ih Hin) <;> simp_all
  | inr Hin =>
  cases Hin with
  | inl Hin => cases (t_ih Hin) <;> simp_all
  | inr Hin => cases (e_ih Hin) <;> simp_all
case eq fn e fn_ih e_ih =>
  cases Hin <;> simp_all
  cases fn_ih <;> simp_all
  cases e_ih <;> simp_all

theorem Lambda.LExpr.substFvarsCorrect :
  WellFormedCoreEvalCong δ →
  Imperative.WellFormedSemanticEvalVar (P:=Expression) δ →
  Imperative.WellFormedSemanticEvalVal (P:=Expression) δ →
  fro.length = to.length →
  Imperative.substDefined σ σ' (fro.zip to) →
  Imperative.substNodup (fro.zip to) →
  Imperative.substStores σ σ' (fro.zip to) →
  to.Disjoint (@Imperative.HasVarsPure.getVars Expression _ _ e) →
  Imperative.invStores σ σ'
    ((@Imperative.HasVarsPure.getVars Expression _ _ e).removeAll (fro ++ to)) →
  δ σ e = δ σ' (e.substFvars (fro.zip $ createFvars to)) := by
  intros Hwfc Hwfvr Hwfvl Hlen Hdef Hnd Hsubst Hnin Hinv
  induction fro generalizing to σ σ' e
  case nil =>
    simp_all
    have Hemp : to = [] := by
      apply List.eq_nil_of_length_eq_zero (Eq.symm Hlen)
    simp [Hemp] at *
    simp [Lambda.LExpr.substFvars]
    exact substFvarsCorrectZero Hwfc Hwfvr Hwfvl Hinv
  case cons h t ih =>
    cases to with
    | nil => simp_all
    | cons h' t' =>
    simp [Lambda.LExpr.substFvars] at *
    simp [createFvars] at *
    have Hsubst1 := substStoresCons' Hnd Hdef Hsubst
    cases Hsubst1 with
    | intro σ₁ Hsubst1 =>
    cases Hsubst1 with
    | intro v₁ Hsubst1 =>
    cases Hsubst1 with
    | intro Hsome Hsubst1 =>
    cases Hsubst1 with
    | intro Hstore Hsubst1 =>
    cases Hsubst1 with
    | intro Hsubst' Hsubst1 =>
    -- the old store can stay unchanged since it is irrelevant
    rw [substFvarCorrect (e := e) Hwfc Hwfvr Hwfvl Hsubst'] <;> simp_all
    rw [ih] <;> try simp_all
    . refine substDefined_updatedState ?_
      exact substDefined_tail Hdef
    . simp [Imperative.substNodup] at Hnd ⊢
      have Hnd2 := nodup_middle Hnd.2
      simp_all
    . -- Disjoint
      intros a' Hin Hin2
      have Hor := getVarsSubstCreateFvar Hin2
      cases Hor <;> simp_all
      next Hin3 =>
        apply @Hnin a' ?_ ?_
        exact List.mem_cons_of_mem h' Hin
        exact Hin3
      next Heq =>
        apply @Hnin h' ?_ ?_
        simp_all
        exfalso
        have Hht := substNodup_ht Hlen Hnd
        simp_all
    . -- invStores from σ₁ to σ'
      rw [subst_create_replace]
      apply invStoresSubstTail Hsubst Hsubst1 Hsome ?_ Hinv
      . simp [Imperative.substNodup] at Hnd
        simp_all
    . simp [List.Disjoint] at Hnin
      exact invStoresSubstHead Hsubst' Hnin.1

/-
theorem createAssertsCorrect :
  Imperative.WellFormedSemanticEvalBool δ →
  Imperative.WellFormedSemanticEvalVar δ →
  Imperative.WellFormedSemanticEvalVal δ →
  -- TODO: remove congruence of old expressions, and require pre to contain no old expressions
  Core.WellFormedCoreEvalCong δ →
  ks.length = ks'.length →
  Imperative.substNodup (ks.zip ks') →
  Imperative.substDefined σA σ' (ks.zip ks') →
  (∀ pre, pre ∈ pres →
    Imperative.invStores σA σ'
      ((Imperative.HasVarsPure.getVars (P:=Expression) pre).removeAll (ks ++ ks')) ∧
    ks'.Disjoint (Imperative.HasVarsPure.getVars (P:=Expression) pre) ∧
    δ σA pre = some Imperative.HasBool.tt) →
  EvalExpressions δ σ (createFvars ks') vals →
  ReadValues σA ks vals →
  Imperative.substStores σ' σA (ks'.zip ks) →
  EvalStatementsContract π δ σ' (createAsserts pres (ks.zip (createFvars ks'))) σ' := by
   intros Hwfb Hwfvr Hwfvl Hwfc Hlen Hnd Hdef Hpres Heval Hrd Hsubst2
   simp [createAsserts]
   -- Make index parameter `i` explicit so that we can induct generalizing `i`.
   suffices h : ∀ (i : Nat) (l : List Expression.Expr),
     (∀ pre, pre ∈ l →
       Imperative.invStores σA σ'
         ((Imperative.HasVarsPure.getVars (P:=Expression) pre).removeAll (ks ++ ks')) ∧
       ks'.Disjoint (Imperative.HasVarsPure.getVars (P:=Expression) pre) ∧
       δ σA pre = some Imperative.HasBool.tt) →
     EvalStatementsContract π δ σ'
       (List.mapIdx (fun j pred => Statement.assert s!"assert_{i + j}"
         (Lambda.LExpr.substFvars pred (ks.zip (createFvars ks')))) l) σ'
   by
    have := @h 0 pres Hpres
    simp at this; exact this
   intros i l Hl
   induction l generalizing i
   case nil =>
     simp; constructor
   case cons st sts ih =>
     simp; constructor; constructor; constructor; constructor
     specialize Hl st (by simp)
     . have Heq : δ σA st = δ σ' (Lambda.LExpr.substFvars st (ks.zip (createFvars ks'))) := by
         apply Lambda.LExpr.substFvarsCorrect Hwfc Hwfvr Hwfvl Hlen Hdef Hnd ?_ Hl.2.1 Hl.1
         . apply Imperative.substStoresFlip'
           simp [Imperative.substSwap, zip_swap]
           assumption
       simp [Imperative.WellFormedSemanticEvalBool] at Hwfb
       rw [← Heq]
       exact Hl.2.2
     . assumption
     . simp [Imperative.isDefinedOver, Command.modifiedVars,
             Imperative.Cmd.modifiedVars,
             Imperative.HasVarsImp.modifiedVars,
             Imperative.isDefined]
     . have ih' := ih (i + 1)
       ac_nf at ih'
       apply ih'
       intros pre Hin
       simp_all

theorem createAssumesCorrect :
  Imperative.WellFormedSemanticEvalBool δ →
  Imperative.WellFormedSemanticEvalVar δ →
  Imperative.WellFormedSemanticEvalVal δ →
  Core.WellFormedCoreEvalCong δ →
  ks.length = ks'.length →
  Imperative.substNodup (ks.zip ks') →
  Imperative.substDefined σA σ' (ks.zip ks') →
  (∀ post, post ∈ posts →
    Imperative.invStores σA σ'
      ((Imperative.HasVarsPure.getVars (P:=Expression) post).removeAll (ks ++ ks')) ∧
    ks'.Disjoint (Imperative.HasVarsPure.getVars (P:=Expression) post) ∧
    δ σA post = some Imperative.HasBool.tt) →
  Imperative.substStores σA σ' (ks.zip ks') →
  EvalStatementsContract π δ σ' (createAssumes posts (ks.zip (createFvars ks'))) σ' := by
   intros Hwfb Hwfvr Hwfvl Hwfc Hlen Hnd Hdef Hposts Hsubst2
   simp [createAssumes]
   -- Make index parameter `i` explicit so that we can induct generalizing `i`.
   suffices h : ∀ (i : Nat) (l : List Expression.Expr),
     (∀ post, post ∈ l →
       Imperative.invStores σA σ'
         ((Imperative.HasVarsPure.getVars (P:=Expression) post).removeAll (ks ++ ks')) ∧
       ks'.Disjoint (Imperative.HasVarsPure.getVars (P:=Expression) post) ∧
       δ σA post = some Imperative.HasBool.tt) →
     EvalStatementsContract π δ σ'
       (List.mapIdx (fun j pred => Statement.assume s!"assume_{i + j}"
         (Lambda.LExpr.substFvars pred (ks.zip (createFvars ks')))) l) σ'
   by
    have := @h 0 posts Hposts
    simp at this; exact this
   intros i l Hl
   induction l generalizing i
   case nil =>
    simp; constructor
   case cons st sts ih =>
    simp ; constructor ; constructor ; constructor ; constructor
    specialize Hl st (by simp)
    . have Heq : δ σA st = δ σ' (Lambda.LExpr.substFvars st (ks.zip (createFvars ks'))) := by
        apply Lambda.LExpr.substFvarsCorrect Hwfc Hwfvr Hwfvl Hlen Hdef Hnd Hsubst2 Hl.2.1 Hl.1
      rw [← Heq]
      exact Hl.2.2
    . assumption
    . simp [Imperative.isDefinedOver, Command.modifiedVars,
            Imperative.Cmd.modifiedVars,
            Imperative.HasVarsImp.modifiedVars,
            Imperative.isDefined]
    . have ih' := ih (i + 1)
      ac_nf at ih'
      apply ih'
      intros post Hin
      simp_all

theorem SubstPostsMem :
  substPost ∈ OldExpressions.substsOldExprs (createOldVarsSubst oldTrips)
  (OldExpressions.normalizeOldExprs vs) →
  ∃ post, post ∈ vs ∧
    substPost = OldExpressions.substsOldExpr (createOldVarsSubst oldTrips) (OldExpressions.normalizeOldExpr post)
  := by
  intros Hin
  generalize Heq : OldExpressions.substsOldExprs
                    (createOldVarsSubst oldTrips)
                    (OldExpressions.normalizeOldExprs vs) = l at *
  cases vs <;> simp [OldExpressions.normalizeOldExprs,
                     OldExpressions.substsOldExprs] at *
  case nil => simp_all
  case cons h t =>
    simp [← Heq] at *
    cases Hin with
    | inl Hin =>
      left; assumption
    | inr Hin =>
      right
      cases Hin with
      | intro id HH => exact ⟨id, HH.1, Eq.symm HH.2⟩
-/

/--
Generate the substitution pairs needed for the body of the procedure
-/
def createOldStoreSubst
  (trips : List ((Expression.Ident × Expression.Ty) × Expression.Ident))
  : List (Expression.Ident × Expression.Ident) :=
    trips.map go where go
    | ((v', _), v) => (v, v')

theorem createOldStoreSubstEq :
  createOldStoreSubst oldTrips =
  oldTrips.unzip.2.zip oldTrips.unzip.1.unzip.1 := by
  induction oldTrips <;> simp [createOldStoreSubst, createOldStoreSubst.go] at *
  case cons h t ih => exact ih

theorem substOldCorrect :
  Imperative.WellFormedSemanticEvalVar δ →
  Imperative.WellFormedSemanticEvalVal δ →
  Core.WellFormedCoreEvalCong δ →
  Core.WellFormedCoreEvalTwoState δ σ₀ σ →
  OldExpressions.NormalizedOldExpr e →
  --Imperative.invStores σ₀ σ
  --  ((OldExpressions.extractOldExprVars e).removeAll [fro]) →
  Imperative.substDefined σ₀ σ [(fro, to)] →
  Imperative.substStores σ₀ σ [(fro, to)] →
  -- substitute the store and the expression simultaneously
  δ σ e = δ σ (OldExpressions.substOld fro (createFvar to) e) := by
  intros Hwfvr Hwfvl Hwfc Hwf2 Hnorm Hdef Hsubst
  induction e <;> simp [OldExpressions.substOld] at *
  case abs m ty e ih  =>
    cases Hnorm with
    | abs Hnorm =>
      apply Hwfc.1
      apply ih Hnorm
  case quant m k ty tr e trih eih =>
    cases Hnorm with
    | quant Ht He =>
      specialize eih He
      specialize trih Ht
      apply Hwfc.quantcongr <;> grind
  case app m c fn fih eih =>
    cases Hnorm with
    | app Hc Hfn Hwf =>
    specialize fih Hc
    specialize eih Hfn
    split
    . -- is an old var
      split
      . -- is an old var that is substituted
        next x ty eq =>
        simp [eq] at *
        simp [WellFormedCoreEvalTwoState] at Hwf2
        cases Hwf2.1 with
        | intro vs Hwf2' =>
        cases Hwf2' with
        | intro vs' Hwf2' =>
        cases Hwf2' with
        | intro σ₁ Hwf2' =>
        by_cases Hin : fro ∈ vs
        case pos =>
        -- old var is modified
          have HH:= Hwf2.2.1 vs vs' σ₀ σ₁ σ Hwf2'.1 Hwf2'.2 fro
          simp [OldExpressions.oldVar,
                OldExpressions.oldExpr,
                CoreIdent.unres, Hin] at HH
          rw [HH]
          simp [createFvar]
          simp [Imperative.WellFormedSemanticEvalVar] at Hwfvr
          rw [Hwfvr (v:=to)]
          apply Hsubst
          exact List.mem_singleton.mpr rfl
          simp [Imperative.HasFvar.getFvar]
        case neg =>
        -- old var is not modified
          have Hup := HavocVarsUpdateStates Hwf2'.1
          cases Hup with
          | intro as Hup =>
          have Hinit := InitVarsInitStates Hwf2'.2
          cases Hinit with
          | intro bs Hinit =>
          have Hsubst' := substStoresUpdatesInv' ?_ Hsubst Hup
          have Hsubst'' := substStoresInitsInv' ?_ Hsubst' Hinit
          . have HH:= Hwf2.2.1 vs vs' σ₀ σ₁ σ Hwf2'.1 Hwf2'.2 fro
            simp [OldExpressions.oldVar,
                  OldExpressions.oldExpr,
                  CoreIdent.unres, Hin] at HH
            simp [createFvar]
            simp [HH]
            simp [Imperative.WellFormedSemanticEvalVar] at Hwfvr
            rw [Hwfvr (v:=to)]
            . simp [Imperative.substStores] at Hsubst''
              exact Hsubst''
            . simp [Imperative.HasFvar.getFvar]
          . simp [Imperative.substDefined] at *
            have Hdef' : Imperative.isDefined σ₀ [fro] := by
              simp [Imperative.isDefined]
              exact Hdef.1
            have Hdef'' := UpdateStatesDefMonotone Hdef' Hup
            simp [Imperative.isDefined] at Hdef''
            refine ⟨Hdef'', Hdef.2⟩
          . simp [List.Disjoint]
            intros a Hin Heq
            simp [Heq] at *
            contradiction
      . -- is an old var that is not substituted, use congruence
        rename_i e1 e2 mOp ty0 mVar x ty1 h
        simp at m mOp ty0 mVar x ty1
        apply Hwfc.appcongr <;> grind
    . -- is not an old var, use congruence
      apply Hwfc.appcongr <;> grind
  case ite m c t e cih tih eih =>
    cases Hnorm with
    | ite Hc Ht He =>
      specialize cih Hc
      specialize tih Ht
      specialize eih He
      apply Hwfc.itecongr <;> grind
  case eq m e1 e2 e1ih e2ih =>
    cases Hnorm with
    | eq He1 He2 =>
    specialize e2ih He2
    apply Hwfc.eqcongr <;> grind


-- Needed from refinement theorem
-- UpdateState P✝ σ id v✝ σ'✝
-- Ht : TouchVars σ'✝ l₂ σ''
-- ⊢ TouchVars σ l₂ σ''

theorem UpdateStatesUpdatedId :
k ∈ vs →
UpdateStates σ₀ vs vs' σ₁ →
UpdateStates (updatedState σ₀ k v) vs vs' σ₁ := by
intros Hin Hup
have Hlen := UpdateStatesLength Hup
induction vs generalizing vs' σ₀ σ₁ k v <;> simp_all
case cons h t ih =>
  cases vs'
  case nil => simp_all
  case cons h' t' =>
    cases Hup with
    | update_some Hup Hups =>
    next σ'' =>
    cases Hin with
    | inl Heq =>
      -- the head is overwritten
      simp [Heq] at *
      apply UpdateStates.update_some (σ':=σ'') ?_ Hups
      constructor
      . simp [updatedState]
        rfl
      . cases Hup
        assumption
      . intros y Hne
        simp [updatedState]
        cases Hup
        split <;> simp_all
    | inr Heq =>
      -- a part of the tail is overwritten
      cases Hup with
      | update Hsome Hall Hsome' =>
      next v' =>
      by_cases Heq : h = k
      case pos =>
        -- both the tail and head are overwritten
        simp [Heq] at *
        apply UpdateStates.update_some (σ':=(updatedState σ'' k h'))
        . constructor
          . simp [updatedState]
            rfl
          . simp [updatedState]
          . intros y Hne
            simp [updatedState]
            split <;> simp_all
        . apply ih <;> simp_all
      case neg =>
        -- only the tail is overwritten
        apply UpdateStates.update_some (σ':=(updatedState σ'' k v))
        . constructor
          . simp [updatedState]
            simp_all
            rfl
          . simp [updatedState]
            simp_all
          . intros y Hne
            simp [updatedState]
            split <;> simp_all
        . apply ih <;> simp_all

theorem InitVarsRemoveAll {P: Imperative.PureExpr} [BEq P.Ident] [LawfulBEq P.Ident]
  {σ σ' : Imperative.SemanticStore P}
  {k : P.Ident} {v : P.Expr} {vs : List P.Ident} :
  σ' k = some v →
  InitVars σ vs σ' →
  InitVars (updatedState (P:=P) σ k v) (List.removeAll vs [k]) σ' := by
intros Hsome Hinit
have HinitSt := InitVarsInitStates Hinit
cases HinitSt with
| intro mv HinitSt =>
have Hnd := InitStatesNodup HinitSt
clear HinitSt
induction vs generalizing σ σ' k v
case nil =>
  simp_all
  simp [InitVarsEmpty Hinit] at *
  rw [updatedStateId] <;> simp_all
case cons h t ih =>
  cases Hinit with
  | init_some Hinit Hinits
  next vv σ₁ =>
  simp only [List.cons_removeAll]
  split
  -- the initialized variable h is not the same as the updated variable k
  . next Hne =>
    simp_all
    apply InitVars.init_some (σ':=(updatedState (updatedState σ k v) h vv))
    apply updatedStateInit
    . simp [updatedState]
      split <;> simp_all
      cases Hinit
      assumption
    . rw [updatedStateComm]
      apply ih Hsome
      have Heq := InitStateUpdated Hinit <;> simp_all
      exact fun a => Hne (Eq.symm a)
  -- the initialized variable h *is* the same as the updated variable k
  . next Heq =>
    -- assert that v = vv, since it has been initialized
    have Heq' : σ₁ k = some v := by
      have Hinitst := InitVarsInitStates Hinits
      cases Hinitst
      case intro t' Hinitst =>
        apply InitStatesSomeMonotone' ?_ Hsome
        apply Hinitst
        simp_all
    have Heq'' : σ₁ k = some vv := by
      have Hrd := InitStateReadValues Hinit
      cases Hrd <;> simp_all
    simp_all
    have Heq''' : (updatedState σ k v) = (updatedState σ₁ k v) := by
      funext x
      simp [updatedState]
      split <;> simp_all
      next Hne =>
      cases Hinit with
      | init Hnone Hsome Hall =>
      rw [Hall]
      exact fun a => Hne (Eq.symm a)
    simp_all

theorem updatedStateOldWellFormedCoreEvalTwoState :
  σ k = some v →
  WellFormedCoreEvalTwoState δ σ₀ σ →
  WellFormedCoreEvalTwoState δ (updatedState σ₀ k v) σ := by
  intros Hsome Hwf2
  simp [WellFormedCoreEvalTwoState] at *
  refine ⟨?_, Hwf2.2⟩
  cases Hwf2.1 with
  | intro vs Hwf2 =>
  cases Hwf2 with
  | intro vs' Hwf =>
  cases Hwf with
  | intro σ₁ Hwf =>
  by_cases Hin : k ∈ vs
  -- k is already in vs, use the mod/init lists as is
  case pos =>
    refine ⟨vs,vs',σ₁,?_,Hwf.2⟩
    have Hup := HavocVarsUpdateStates Hwf.1
    cases Hup with
    | intro vs' Hup =>
    apply UpdateStatesHavocVars (modvals:=vs')
    exact UpdateStatesUpdatedId Hin Hup
  -- k is not in vs, add k to vs
  case neg =>
    by_cases Hin' : k ∈ vs'
    -- k not in vs, but is in vs'.
    -- This is the case that k is a newly created variable
    -- Since we are updating/initializing k in σ₀, we remove k from vs'
    case pos =>
      refine ⟨vs,vs'.removeAll [k],(updatedState σ₁ k v),?_,?_⟩
      . refine HavocVarsUpdatedDist Hin ?_
        exact Hwf.1
      . apply InitVarsRemoveAll <;> simp_all
    -- k is not in vs'
    case neg =>
      have Hup := HavocVarsUpdateStates Hwf.1
      cases Hup with
      | intro es' Hup =>
      refine ⟨k :: vs,vs',σ₁,?_,Hwf.2⟩
      have Hdef1 : Imperative.isDefined σ₁ [k] := by
        apply InitVarsDefMonotone' (σ':=σ) (vs':=vs') <;> simp_all
        . simp_all [List.Disjoint]
        . simp [Imperative.isDefined, Option.isSome]
          split <;> simp_all
      have Hdef0 : Imperative.isDefined σ₀ [k] := by
        exact HavocVarsDefMonotone' (vs':=vs) Hdef1 Hwf.1
      simp [Imperative.isDefined, Option.isSome] at Hdef0
      split at Hdef0 <;> simp_all
      next x val heq =>
      apply UpdateStatesHavocVars (modvals:=val :: es')
      refine UpdateStatesUpdatedId ?_ ?_
      . exact List.mem_cons_self
      . apply UpdateStates.update_some (σ':=updatedState σ₀ k val)
        apply updatedStateUpdate <;> assumption
        rw [updatedStateId] <;> simp_all

open OldExpressions in
theorem extractedOldExprInVars :
  NormalizedOldExpr post →
  (extractOldExprVars post).Subset
  (Imperative.HasVarsPure.getVars post) := by
  have join {a b c d : List Expression.Ident} (h : a.Subset b) (k : c.Subset d) :
      (a ++ c).Subset (b ++ d) :=
    fun _ hx => List.mem_append.mpr ((List.mem_append.mp hx).imp (fun hx => h hx) (fun hx => k hx))
  intro h
  induction h <;>
    simp only [Imperative.HasVarsPure.getVars, extractOldExprVars,
      Lambda.LExpr.LExpr.getVars] at * <;>
    try solve | exact List.Subset.empty | assumption | exact join ‹_› ‹_›
              | exact join (join ‹_› ‹_›) ‹_›
  case app hn he hold ih₁ ih₂ =>
    unfold extractOldExprVars
    split
    · exact fun _ h => h
    · next hf =>
      have hv := hold .oldPred
      cases hv
      exfalso
      apply hf
      rfl
    · exact join ih₁ ih₂
